plugins {
    id 'java'
    id 'maven-publish'
}

group 'com.github.reugn'
version '0.3.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

publishing {
    repositories {
        maven {
            name = "GithubPackages"
            url = uri("https://maven.pkg.github.com/reugn/kafka-aerospike-state-store")
            credentials {
                username = properties.getProperty("user") ?: System.getenv("GITHUB_ACTOR")
                password = properties.getProperty("password") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

ext {
    kafkaVersion = '2.8.1'
    junitVersion = '5.8.1'
}

dependencies {
    implementation "org.apache.kafka:kafka-streams:$kafkaVersion"
    implementation "com.aerospike:aerospike-client:5.1.8"
    implementation "io.netty:netty-all:4.1.68.Final"
    implementation "org.apache.commons:commons-lang3:3.12.0"
    testImplementation "org.apache.kafka:kafka-streams-test-utils:$kafkaVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

test {
    useJUnitPlatform()
}
